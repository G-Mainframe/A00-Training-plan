<html>

<head>
<title>4.5 集計表形式の帳票定義体を利用した記述例</title>
</head>

<body BGCOLOR="#FFFFFF">

<table WIDTH="100%" BORDER="0" CELLSPACING="0" CELLPADDING="0">
  <tr>
    <td ALIGN="LEFT"><strong>帳票印刷コントロール使用手引書</strong> 
    </td>
  </tr>
  <tr>
    <td ALIGN="RIGHT"><a HREF="../index.htm" TARGET="_top">目次</a>&nbsp; <a href="pctl0440.htm"><img SRC="../pctlosrc/previous.gif" BORDER="0" ALT="前ページ" width="56" height="21"></a><a href="pctl0460.htm"><img SRC="../pctlosrc/next.gif" BORDER="0" ALT="次ページ" width="56" height="21"></a> </td>               
  </tr>               
</table>               
               
<hr>               
               
<dl>               
  <dl>               
    <dt><a HREF="pctl0400.htm"><img SRC="../pctlosrc/up.gif" BORDER="0" ALT="上へ" WIDTH="16" HEIGHT="15">第4章                 
      帳票印刷のアプリケーションを作成する</a> </dt>                
  </dl>                
</dl>                
                
<hr>                
                
<h2>4.5 集計表形式の帳票定義体を利用した記述例</h2>              
              
<p>ここでは集計表形式の帳票定義体を利用した記述例を説明します。                  
</p>                 
                 
<p><img SRC="../pctlosrc/box1poin.gif" ALT="image" WIDTH="47" HEIGHT="41">　<strong>[Microsoft(R)              
VisualBasic(R)の記述例]</strong></p>                    
                    
<blockquote>                    
  <p>集計表形式の帳票定義体を利用した記述例です。<br>                
  コマンドボタンのクリックイベントで、帳票印刷コントロール(PowerFormReport1)の「帳票印刷(PrintControl)」メソッドを呼び出します。<br>             
  データベースを参照しながらレコードデータの出力を行います。<br>             
  また、読み込んだレコードデータをもとに、就業実績を計算します。</p>           
  <p>総ページ数を出力するために、｢印刷種別(PrintType)｣プロパティに｢2:通常印刷2回読み｣を指定しています。2回読みの1回目を処理しているか2回目を処理しているかは、｢印刷回数(PrintCount)｣プロパティを使用して判断します。</p>         
  <p><img border="0" src="../pctlosrc/boxchuui.gif" width="47" height="31"></p>         
          
<ul>          
  <li>空のパーティション（項目や罫線/網がけなど何も定義していないパーティション）を出力した場合、そのパーティションは印刷されません。ただし、各イベントは発生します。<br> 
    以下のサンプルで使用する帳票定義体では、制御頭書きイベントを発生させるために、空の制御頭書きに対してコントロールブレークを定義しています。<br> 
  </li>          
  <li>明細計算(CalculateDE)イベントで明細を集計します。コントロールブレークの発生に伴い、制御頭書き(CBHead)イベントで明細を初期化した場合、それまでの集計結果は初期化されます。<br>  
  処理対象の明細に対しての再計算を行う必要がある場合は、明細出力(PrintDE)イベントで再集計処理を行います。    
  </li>          
</ul>         
         
</blockquote>         
         
<table BORDER="1" WIDTH="74%">         
  <tr>         
    <td><pre>'外部定義変数
Dim DaoDB1
Dim DaoRec1

'CommandButtonのClickイベント
Private Sub Command1_Click()
    'コマンドボタンを選択不可にする
    Command1.Enabled = False

    '印刷ジョブのタイトルを設定
    PowerFormReport1.PrintJobName = &quot;集計表形式　就業実績&quot;
    
    '二度読みを設定
    PowerFormReport1.PrintType = 2
    
    '帳票印刷メソッドの実行
    PowerFormReport1.PrintControl (0)

    'コマンドボタンを選択可能にする
    Command1.Enabled = True
End Sub


Private Sub PowerFormReport1_CalculateDE(ByVal PrintRecord As Long)
    '明細データの計算をする
    Dim 所定内 As Integer
    Dim 残業 As Integer
    Dim 休暇遅刻 As Integer
    Dim 休憩 As Integer
    Dim 休日出勤 As Integer
        
    Dim 出社時間 As Integer
    Dim 退社時間 As Integer
    Dim 休日 As Boolean
        
    所定内 = 0
    残業 = 0
    休暇遅刻 = 0
    休憩 = 0
    休日出勤 = 0
    出社時間 = 0
    退社時間 = 0
    
    '勤務時間    9:00〜17:30 7.5時間労働
    'コアタイム  勤務時間帯の任意3時間
    '残業時間   18:00〜22:00
    '休憩時間   12:00〜13:00,17:30〜18:00
    
    '休日かの確認
    If PowerFormReport1.FieldText(&quot;休み&quot;, -1, 0, 0) = &quot;休日&quot; Then
      休日 = True
    Else
      休日 = False
    End If
    
    '出社時間／退社時間の加工(15分単位)
    出社時間 = Val(PowerFormReport1.FieldText(&quot;出社時間&quot;, -1, 0, 0))
    退社時間 = Val(PowerFormReport1.FieldText(&quot;退社時間&quot;, -1, 0, 0))
    
    If 出社時間 &lt; 900 Then
        出社時間 = 900
    ElseIf 1200 &lt;= 出社時間 And 出社時間 &lt; 1300 Then
        出社時間 = 1300
    ElseIf 1750 &lt;= 出社時間 And 出社時間 &lt; 1800 Then
        出社時間 = 1800
    Else
        出社時間 = ((出社時間 + 24) \ 25) * 25
    End If
    
    If 退社時間 &lt; 900 Then
        出社時間 = 0
        退社時間 = 0
    ElseIf 1200 &lt;= 退社時間 And 退社時間 &lt; 1300 Then
        退社時間 = 1200
    ElseIf 1750 &lt;= 退社時間 And 退社時間 &lt; 1800 Then
        退社時間 = 1750
    Else
        退社時間 = (退社時間 \ 25) * 25
    End If
    
    If 退社時間 &lt; 出社時間 Then
        出社時間 = 0
        退社時間 = 0
    End If
    
    '休みかの確認(退社が0の時はお休み)
    If 退社時間 = 0 Then
        If 休日 = False Then
            休暇遅刻 = 800
        End If
        GoTo DATA_SETEI
    End If
    
    '残業時間の計算
    If 出社時間 &lt;= 1800 Then
        残業 = 退社時間 - 1800
    Else
        残業 = 退社時間 - 出社時間
    End If
    
    '所定内勤務時間の計算
    If 1800 &lt; 退社時間 Then
        所定内 = 1750 - 出社時間
    Else
        所定内 = 退社時間 - 出社時間
    End If
    
    '休憩時間の計算 および
    '所定内時間から休憩時間を引く
    If 出社時間 &lt;= 1200 And 1300 &lt;= 退社時間 Then
        休憩 = 100
        所定内 = 所定内 - 休憩
    End If
    If syusya &lt;= 1750 And 1800 &lt;= 退社時間 Then
        休憩 = 休憩 + 50
    End If
    
    '遅刻の計算
    If 所定内 &lt; 400 Then
        休暇遅刻 = 400 - 所定内
    End If
    
    '休日出勤の計算
    If 休日 = True Then
        休日出勤 = 所定内 + 残業
        Syotei = 0
        残業 = 0
        休暇遅刻 = 0
    End If
       
DATA_SETEI:
    PowerFormReport1.FieldText(&quot;所定内&quot;, -1, 0, 0) = 所定内
    PowerFormReport1.FieldText(&quot;残業&quot;, -1, 0, 0) = 残業
    PowerFormReport1.FieldText(&quot;休日出勤&quot;, -1, 0, 0) = 休日出勤
    PowerFormReport1.FieldText(&quot;休暇遅刻&quot;, -1, 0, 0) = 休暇遅刻
    PowerFormReport1.FieldText(&quot;休憩&quot;, -1, 0, 0) = 休憩
    
End Sub


Private Sub PowerFormReport1_CBHead(ByVal PrintRecord As Long, ByVal BrkFldName As String)
    '処理中社員に関する情報をクリアする
    If BrkFldName = &quot;社員番号&quot; Then
        PowerFormReport1.FieldText(&quot;総所定内&quot;, -1, 0, 0) = 0
        PowerFormReport1.FieldText(&quot;総残業&quot;, -1, 0, 0) = 0
        PowerFormReport1.FieldText(&quot;総休日出勤&quot;, -1, 0, 0) = 0
        PowerFormReport1.FieldText(&quot;総休暇遅刻&quot;, -1, 0, 0) = 0
        PowerFormReport1.FieldText(&quot;総休憩&quot;, -1, 0, 0) = 0
        
    ElseIf BrkFldName = &quot;月度&quot; Then
        PowerFormReport1.FieldText(&quot;月所定内&quot;, -1, 0, 0) = 0
        PowerFormReport1.FieldText(&quot;月残業&quot;, -1, 0, 0) = 0
        PowerFormReport1.FieldText(&quot;月休日出勤&quot;, -1, 0, 0) = 0
        PowerFormReport1.FieldText(&quot;月休暇遅刻&quot;, -1, 0, 0) = 0
        PowerFormReport1.FieldText(&quot;月休憩&quot;, -1, 0, 0) = 0
    End If
    
End Sub


'帳票印刷コントロールの帳票閉鎖(CloseReport)イベント
Private Sub PowerFormReport1_CloseReport()
    '印刷で利用したデータベースのクローズ
    DaoRec1.Close
    DaoDB1.Close
End Sub


'帳票印刷コントロールの帳票開設(OpenReport)イベント
Private Sub PowerFormReport1_OpenReport()
    '印刷で利用するデータベースのオープン
    'データベース名は、データファイルディレクトリ配下のデータファイル.mdb
    'クエリ名は、就業実績
    Set DaoDB1 = DaoWS.OpenDatabase((App.Path &amp; &quot;\データファイル\データファイル.mdb&quot;), False)
    Set DaoRec1 = DaoDB1.OpenRecordset(&quot;SELECT * FROM 就業実績&quot;)
    DaoRec1.MoveFirst

    '帳票定義体名、ディレクトリ名を帳票コントロール
    'のプロパティ、「FormName」、「FormDirectory」に設定します。
    'ディレクトリは、アプリ実行配下の帳票フォームのディレクトリとします。
    PowerFormReport1.FormDirectory = App.Path &amp; &quot;\帳票フォーム&quot;
    PowerFormReport1.FormName = &quot;Report.pxd&quot;

    'メディア格納ディレクトリの設定
    PowerFormReport1.MediaDirectory = App.Path &amp; &quot;\メディアファイル&quot;
End Sub


Private Sub PowerFormReport1_PageFoot(ByVal PrintRecord As Long)
    'ページ数を設定する
    PowerFormReport1.FieldText(&quot;ページ&quot;, -1, 0, 0) = PowerFormReport1.PrintingPageNumber
    
End Sub


Private Sub PowerFormReport1_PrintDE(ByVal PrintRecord As Long)

    '月度に対する集計
    PowerFormReport1.FieldText(&quot;月所定内&quot;, -1, 0, 0) = Val(PowerFormReport1.FieldText(&quot;月所定内&quot;, -1, 0, 0)) + Val(PowerFormReport1.FieldText(&quot;所定内&quot;, -1, 0, 0))
    PowerFormReport1.FieldText(&quot;月残業&quot;, -1, 0, 0) = Val(PowerFormReport1.FieldText(&quot;月残業&quot;, -1, 0, 0)) + Val(PowerFormReport1.FieldText(&quot;残業&quot;, -1, 0, 0))
    PowerFormReport1.FieldText(&quot;月休日出勤&quot;, -1, 0, 0) = Val(PowerFormReport1.FieldText(&quot;月休日出勤&quot;, -1, 0, 0)) + Val(PowerFormReport1.FieldText(&quot;休日出勤&quot;, -1, 0, 0))
    PowerFormReport1.FieldText(&quot;月休暇遅刻&quot;, -1, 0, 0) = Val(PowerFormReport1.FieldText(&quot;月休暇遅刻&quot;, -1, 0, 0)) + Val(PowerFormReport1.FieldText(&quot;休暇遅刻&quot;, -1, 0, 0))
    PowerFormReport1.FieldText(&quot;月休憩&quot;, -1, 0, 0) = Val(PowerFormReport1.FieldText(&quot;月休憩&quot;, -1, 0, 0)) + Val(PowerFormReport1.FieldText(&quot;休憩&quot;, -1, 0, 0))
        
    '総月度に対する集計
    PowerFormReport1.FieldText(&quot;総所定内&quot;, -1, 0, 0) = Val(PowerFormReport1.FieldText(&quot;総所定内&quot;, -1, 0, 0)) + Val(PowerFormReport1.FieldText(&quot;所定内&quot;, -1, 0, 0))
    PowerFormReport1.FieldText(&quot;総残業&quot;, -1, 0, 0) = Val(PowerFormReport1.FieldText(&quot;総残業&quot;, -1, 0, 0)) + Val(PowerFormReport1.FieldText(&quot;残業&quot;, -1, 0, 0))
    PowerFormReport1.FieldText(&quot;総休日出勤&quot;, -1, 0, 0) = Val(PowerFormReport1.FieldText(&quot;総休日出勤&quot;, -1, 0, 0)) + Val(PowerFormReport1.FieldText(&quot;休日出勤&quot;, -1, 0, 0))
    PowerFormReport1.FieldText(&quot;総休暇遅刻&quot;, -1, 0, 0) = Val(PowerFormReport1.FieldText(&quot;総休暇遅刻&quot;, -1, 0, 0)) + Val(PowerFormReport1.FieldText(&quot;休暇遅刻&quot;, -1, 0, 0))
    PowerFormReport1.FieldText(&quot;総休憩&quot;, -1, 0, 0) = Val(PowerFormReport1.FieldText(&quot;総休憩&quot;, -1, 0, 0)) + Val(PowerFormReport1.FieldText(&quot;休憩&quot;, -1, 0, 0))
    
End Sub


'帳票印刷コントロールのレコード読込み(ReadRecord)イベント
Private Sub PowerFormReport1_ReadRecord(ByVal PrintRecord As Long)
    Dim FiledCount
    Dim FiledName

    If PowerFormReport1.RecordCount &gt; 1 Then
        '２件目以降のレコードを使用するため、次レコードに移動
       DaoRec1.MoveNext
    End If

    If DaoRec1.EOF Then
        '最終レコードまで処理したのでＥＯＦ設定
        PowerFormReport1.RecordEOF = True
        GoTo EndSub:
    End If
    
    'データベースの対応する項目からデータを転記する
    For FiledCount = 0 To DaoRec1.Fields.Count - 1
        '読み込むデータデースのフィールド名を調べる
        FiledName = DaoRec1.Fields(FiledCount).Name
        'データが指定されているかどうか調べる
        If DaoRec1.Fields(FiledName) &lt;&gt; 0 Then
            'データがあるので転記する
            PowerFormReport1.FieldText(FiledName, -1, 0, 0) = DaoRec1.Fields(FiledName)
        Else
            'データがないので初期化する
            If PowerFormReport1.FieldType(FiledName, -1, 0, 0) = 2 Then
                PowerFormReport1.FieldText(FiledName, -1, 0, 0) = &quot;0&quot;
            Else
                PowerFormReport1.FieldText(FiledName, -1, 0, 0) = &quot;&quot;
            End If
        End If
    Next FiledCount

EndSub:

End Sub


'フォームモジュールのロードイベント
Private Sub Form_Load()
    'ＤＡＯワークスペースのオープン
    Set DaoWS = DBEngine.Workspaces(0)
    'カレントディレクトリの変更
    ChDir (App.Path &amp; &quot;\データファイル&quot;)

End Sub


'フォームモジュールの終了イベント
Private Sub Form_Terminate()
    'ＤＡＯワークスペースのクローズ
    DaoWS.Close
End Sub


Private Sub PowerFormReport1_ReportFoot(ByVal PrintRecord As Long)

    '総ページ数を設定する
    If PowerFormReport1.PrintCount = 1 Then
        PowerFormReport1.FieldText(&quot;総ページ&quot;, -1, 0, 0) = PowerFormReport1.PrintingPageNumber
    End If

End Sub


Private Sub PowerFormReport1_ReportHead(ByVal PrintRecord As Long)
    '集計情報の転記先をクリアする
    PowerFormReport1.FieldText(&quot;総所定内&quot;, -1, 0, 0) = 0
    PowerFormReport1.FieldText(&quot;総残業&quot;, -1, 0, 0) = 0
    PowerFormReport1.FieldText(&quot;総休日出勤&quot;, -1, 0, 0) = 0
    PowerFormReport1.FieldText(&quot;総休暇遅刻&quot;, -1, 0, 0) = 0
    PowerFormReport1.FieldText(&quot;総休憩&quot;, -1, 0, 0) = 0
    
    PowerFormReport1.FieldText(&quot;月所定内&quot;, -1, 0, 0) = 0
    PowerFormReport1.FieldText(&quot;月残業&quot;, -1, 0, 0) = 0
    PowerFormReport1.FieldText(&quot;月休日出勤&quot;, -1, 0, 0) = 0
    PowerFormReport1.FieldText(&quot;月休暇遅刻&quot;, -1, 0, 0) = 0
    PowerFormReport1.FieldText(&quot;月休憩&quot;, -1, 0, 0) = 0

End Sub
</pre>         
    </td>         
  </tr>         
</table>         
         
<p><a href="../pctlosrc/sample/Report01.exe">サンプルプログラムのダウンロード</a></p>        
        
<hr>       
       
<table WIDTH="100%" BORDER="0" CELLSPACING="0" CELLPADDING="0">       
  <tr>       
    <td ALIGN="RIGHT"><a HREF="../index.htm" TARGET="_top">目次</a>&nbsp; <a href="pctl0440.htm"><img SRC="../pctlosrc/previous.gif" BORDER="0" ALT="前ページ" width="56" height="21"></a><a href="pctl0460.htm"><img SRC="../pctlosrc/next.gif" BORDER="0" ALT="次ページ" WIDTH="56" HEIGHT="21"></a> </td>              
  </tr>              
</table>              
              
<hr>              
              
<p align="right">All Rights Reserved, Copyright (C) 富士通株式会社 
1998-2001</p>              
</body>              
</html>              
